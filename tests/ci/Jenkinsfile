// Copyright (c) 2022, Oracle and/or its affiliates.
//
// Licensed under the Universal Permissive License v 1.0 as shown at https://oss.oracle.com/licenses/upl/
//

pipeline {
	agent {
		node {
			label 'operator-ci'
		}
	}
	parameters {
		string(name: 'OPERATOR_GIT_REVISION', description: 'a git revision or branch to run against')
		string(name: 'OPERATOR_IMAGE', description: 'an image related to the aforementioned git revision')
		booleanParam(name: 'OPERATOR_INTERNAL_BUILD', defaultValue: false, description: 'triggered by us (e.g. by a push to a dev branch) or externally (e.g. from the concourse)')
	}
	environment {
		WORKERS_FOLDER = 'Shell/KubernetesOperator/workers'
		// WORKERS_FOLDER = 'Shell/KubernetesOperator/sandbox'
		BUILD_TRIGGERED_BY = "${params.OPERATOR_INTERNAL_BUILD ? 'internal' : 'concourse'}"
		LOG_SUBDIR = "build-${BUILD_NUMBER}"
		LOG_DIR = "${WORKSPACE}/${LOG_SUBDIR}"
		ARTIFACT_FILENAME = "result-${JOB_BASE_NAME}-${BUILD_NUMBER}.tar.bz2"
		ARTIFACT_PATH = "${WORKSPACE}/${ARTIFACT_FILENAME}"
		// SLACK_CHANNEL = '#mysql-operator-ci'
		SLACK_CHANNEL = '#mysql-operator-dev'
		BUILD_NOTIFICATION_HEADER = "${currentBuild.fullDisplayName} (<${env.BUILD_URL}|Open>)"
	}
 	options {
		timeout(time: 2, unit: 'HOURS')
		timestamps()
		copyArtifactPermission("${env.WORKERS_FOLDER}/k3d,${env.WORKERS_FOLDER}/minikube,${env.WORKERS_FOLDER}/prepare-test-suite-report")
	}
	stages {
		stage('init') {
			steps {
				script {
					env.GIT_AUTHOR_DATE = sh (script: "git log -1 --pretty='%an <%ae>, %ad' ${GIT_COMMIT}", returnStdout: true).trim()
					env.GIT_BRANCH_NAME = sh (script: "git name-rev --name-only ${GIT_COMMIT}", returnStdout: true).trim()
					env.GIT_COMMIT_SUBJECT = sh (script: "git log -1 --pretty=%s ${GIT_COMMIT}", returnStdout: true).trim()
					env.GIT_COMMIT_SHORT = sh (script: "git rev-parse --short HEAD", returnStdout: true).trim()
				}

				slackSend channel: "${env.SLACK_CHANNEL}",
					color: "warning",
					sendAsText: true,
					message: """${env.BUILD_NOTIFICATION_HEADER}
${currentBuild.getBuildCauses().shortDescription} (${env.BUILD_TRIGGERED_BY})
Branch/Revision: ${env.GIT_BRANCH_NAME} ${params.OPERATOR_GIT_REVISION}
Image: ${params.OPERATOR_IMAGE}
The latest commit:
${env.GIT_AUTHOR_DATE}
${env.GIT_COMMIT} [hash: ${env.GIT_COMMIT_SHORT}]
${env.GIT_COMMIT_SUBJECT}"""

				sh 'mkdir -p ${LOG_DIR}'

				build job: "${env.WORKERS_FOLDER}/init", parameters: [
					string(name: 'OPERATOR_GIT_REVISION', value: params.OPERATOR_GIT_REVISION),
					string(name: 'OPERATOR_IMAGE', value: params.OPERATOR_IMAGE),
					booleanParam(name: 'OPERATOR_INTERNAL_BUILD', value: params.OPERATOR_INTERNAL_BUILD)
				]
			}
		}
		stage('build') {
			when {
				expression { params.OPERATOR_INTERNAL_BUILD }
			}
			steps {
				build job: "${env.WORKERS_FOLDER}/build", parameters: [
					string(name: 'OPERATOR_GIT_REVISION', value: params.OPERATOR_GIT_REVISION),
					string(name: 'OPERATOR_IMAGE', value: params.OPERATOR_IMAGE),
					booleanParam(name: 'OPERATOR_INTERNAL_BUILD', value: params.OPERATOR_INTERNAL_BUILD)
				]
			}
		}
		stage('test') {
			parallel {
				stage('minikube') {
					steps {
						echo "minikube"
						build job: "${env.WORKERS_FOLDER}/minikube", parameters: [
							string(name: 'OPERATOR_GIT_REVISION', value: params.OPERATOR_GIT_REVISION),
							string(name: 'OPERATOR_IMAGE', value: params.OPERATOR_IMAGE),
							booleanParam(name: 'OPERATOR_INTERNAL_BUILD', value: params.OPERATOR_INTERNAL_BUILD)
						]
					}
					post {
						always {
							echo "minikube results"
							copyArtifacts fingerprintArtifacts: true,
								projectName: "${env.WORKERS_FOLDER}/minikube",
								selector: lastCompleted(),
								target: "${LOG_SUBDIR}"
						}
						cleanup {
							script {
								if (addTestResults("minikube")) {
									env.MINIKUBE_RESULTS_AVAILABLE = true
								}
							}
						}
					}
				}
				stage('k3d') {
					// with agent set, some files will be stored in $WORKSPACE_TMP
					// agent {
					// 	label "operator-ci"
					// }
					steps {
						echo "k3d"

						// run k3d clusters with some delay, after all minikube clusters already have started
						// when clusters start at the same moment, they may fail randomly
						sleep 300
						build job: "${env.WORKERS_FOLDER}/k3d", parameters: [
							string(name: 'OPERATOR_GIT_REVISION', value: params.OPERATOR_GIT_REVISION),
							string(name: 'OPERATOR_IMAGE', value: params.OPERATOR_IMAGE),
							booleanParam(name: 'OPERATOR_INTERNAL_BUILD', value: params.OPERATOR_INTERNAL_BUILD)
						]
					}
					post {
						always {
							echo "k3d post"
							copyArtifacts fingerprintArtifacts: true,
								projectName: "${env.WORKERS_FOLDER}/k3d",
								selector: lastCompleted(),
								target: "${LOG_SUBDIR}"
						}
						cleanup {
							script {
								if (addTestResults("k3d")) {
									env.K3D_RESULTS_AVAILABLE = true
								}
							}
						}
					}
				}
			}
			post {
				always {
					script {
						if (anyResultsAvailable()) {
							build job: "${env.WORKERS_FOLDER}/prepare-test-suite-report", parameters: [
								string(name: 'OPERATOR_GIT_REVISION', value: params.OPERATOR_GIT_REVISION),
								string(name: 'TEST_RESULT_BUILD_URL', value: env.BUILD_URL)
							]

							copyArtifacts fingerprintArtifacts: true,
								projectName: "${env.WORKERS_FOLDER}/prepare-test-suite-report",
								selector: lastCompleted(),
								target: "${LOG_SUBDIR}"

							env.TEST_SUITE_REPORT = getTestSuiteReport()
						} else {
							env.TEST_SUITE_REPORT = ""
						}
					}
				}
				cleanup {
					script {
						sh 'ls -lRF ${LOG_DIR}'
						env.TESTS_STATUS_HEADER = "Test suite:"
						if (anyResultsAvailable()) {
							if (!env.MINIKUBE_RESULTS_AVAILABLE) {
								env.TESTS_STATUS_HEADER += "\nNo test results for minikube!"
							}
							if (!env.K3D_RESULTS_AVAILABLE) {
								env.TESTS_STATUS_HEADER += "\nNo test results for k3d!"
							}
						} else {
							env.TESTS_STATUS_HEADER = "\nNo test results available!"
						}
					}
				}
			}
		}
	}
	post {
		always {
			script {
				env.BUILD_DURATION = "${currentBuild.durationString.minus(' and counting')}"
				env.CHANGE_LOG = getChangeLog()

				sh 'ls -lRF ${LOG_DIR}'
				if (anyResultsAvailable()) {
					sh "cd ${LOG_DIR} && tar cvjf ${ARTIFACT_PATH} *"
					archiveArtifacts artifacts: "${ARTIFACT_FILENAME}", fingerprint: true
				}
			}
		}
		changed {
			script {
				modifyBuildStatus("changed")
			}
		}
		fixed {
			script {
				modifyBuildStatus("fixed")
			}
		}
		unstable {
			script {
				modifyBuildStatus("unstable")
			}
		}
		regression {
			script {
				modifyBuildStatus("regression")
			}
		}
		failure {
			script {
				modifyBuildStatus("failure")
			}
		}
		aborted {
			script {
				modifyBuildStatus("aborted")
			}
		}
		unsuccessful {
			script {
				modifyBuildStatus("unsuccessful")
			}
		}
		success {
			script {
				modifyBuildStatus("success")
			}
		}
		cleanup {
			slackSend channel: "${env.SLACK_CHANNEL}",
				sendAsText: true,
				color: "good",
					message: """${env.BUILD_NOTIFICATION_HEADER}
Status: ${env.BUILD_STATUS}
Duration: ${env.BUILD_DURATION}
${env.CHANGE_LOG}
${env.TESTS_STATUS_HEADER}
${env.TEST_SUITE_REPORT}"""
		}
	}
}

def addTestResults(String k8s_env) {
	sh "ls ${env.LOG_DIR}"
	def testResults = findFiles glob: "**/${env.LOG_SUBDIR}/result-$k8s_env-*.tar.bz2"
	if (testResults.length == 0) {
		return false
	}

	def resultPattern = "${env.LOG_DIR}/result-$k8s_env-*.tar.bz2"
	sh "cat $resultPattern | tar jxvf - -i -C ${env.LOG_DIR} && rm $resultPattern"

	// uncomment during Jenkins refactorings when some jobs are intentionally skipped
	// sh "touch ${LOG_DIR}/xml/*.xml"

	def summary = junit allowEmptyResults: true, testResults: "${env.LOG_SUBDIR}/xml/$k8s_env-*.xml"
	echo "${summary.totalCount} tests, ${summary.passCount} passed, ${summary.failCount} failed, ${summary.skipCount} skipped"
	return true
}

def anyResultsAvailable() {
	return env.MINIKUBE_RESULTS_AVAILABLE || env.K3D_RESULTS_AVAILABLE;
}

def getTestSuiteReport() {
	sh "ls ${env.LOG_DIR}"
	def testSuiteReportFiles = findFiles glob: "**/${env.LOG_SUBDIR}/test_suite_report_*.tar.bz2"
	if (testSuiteReportFiles.length == 0) {
		return ""
	}

	def reportPattern = "${env.LOG_DIR}/test_suite_report_*.tar.bz2"
	sh "cat $reportPattern | tar jxvf - -i -C ${env.LOG_DIR} && rm $reportPattern"

	def reportPath = "${env.LOG_DIR}/test_suite_report.txt"
	def reportExists = fileExists reportPath
	if (!reportExists) {
		return ""
	}

	def briefReportPath = "${env.LOG_DIR}/test_suite_brief_report.txt"
	def ReportedIssuesMaxCount = 8
	sh "cat $reportPath | sed -ne '1,$ReportedIssuesMaxCount p' -e '${ReportedIssuesMaxCount+1} iand more...' > $briefReportPath"

	def testSuiteReport = readFile(file: briefReportPath)
	echo testSuiteReport
	return testSuiteReport
}

@NonCPS
def getChangeLog() {
	def changeSets = currentBuild.changeSets
	if (!changeSets.size()) {
		return "No changes\n"
	}

	def changeLog = "Changes:\n"
	for (int i = 0; i < changeSets.size(); ++i) {
		def entries = changeSets[i].items
		for (int j = 0; j < entries.length; ++j) {
			def entry = entries[j]
			changeLog += "${entry.msg} [${entry.author}, ${new Date(entry.timestamp)}]\n"
		}
	}
	return changeLog
}

@NonCPS
def modifyBuildStatus(String status) {
	if (!env.BUILD_STATUS) {
		env.BUILD_STATUS = status
	} else {
		env.BUILD_STATUS += ", " + status
	}
}
